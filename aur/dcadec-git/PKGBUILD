# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Yamashita Ren <lemaitre.lotus@gmail.com>

pkgname=dcadec-git
pkgver=r211.81121d1
pkgrel=1
pkgdesc='DTS Coherent Acoustics decoder with support for HD extensions'
arch=('armv6h' 'armv7h')
url='https://github.com/foo86/dcadec.git'
license=('LGPL2.1')
depends=('glibc')
conflicts=()
provides=('dcadec')
makedepends=('git')
source=('git://github.com/foo86/dcadec.git')
md5sums=('SKIP')

pkgver() {
  cd dcadec

  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd dcadec

  export CONFIG_SHARED='TRUE'

  make -j 4
}

package() {
  cd dcadec

  export CONFIG_SHARED='TRUE'
  export PREFIX='/usr'

  make DESTDIR="${pkgdir}" install
  mv "${pkgdir}"/usr/bin/{dcadec,dcahddec}
}

# vim: ts=2 sw=2 et:
