# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgname=gst-imx
_commit=0c8c284f598c777e14ecec6be72aea836f774ad8
pkgver=0.9.7
pkgrel=2
pkgdesc="GStreamer plugins for i.MX platforms"
arch=('armv7h')
license=('LGPL')
url="https://github.com/Freescale/gstreamer-imx"
depends=('gst-plugins-base-libs' 'libfslvpuwrap' 'gpu-viv-bin-mx6q-fb')
makedepends=('python2' 'linux-headers-imx6-fsl')
source=("https://github.com/Freescale/gstreamer-imx/archive/${_commit}.tar.gz")
md5sums=('fb1f4982bca844e305255b68f9e47bab')

LDFLAGS+=' -L/opt/fsl/lib'
CPPFLAGS+=' -I/opt/fsl/include'

build() {
  cd gstreamer-imx-${_commit}

  export PKG_CONFIG_PATH='/opt/fsl/lib/pkgconfig'

  python2 waf configure --prefix=/usr \
    --with-package-name="GStreamer plugins for i.MX platforms" \
    --with-package-origin="https://github.com/archlinuxarm/PKGBUILDs/tree/master/alarm/gst-imx"
  
  python2 waf build $MAKEFLAGS
}

package() {
  cd gstreamer-imx-${_commit}
  python2 waf install --destdir="$pkgdir"
}
