# U-Boot for i.MX233-OLinuXino
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>
# Contributor: Kiril Zyapkov <kiril.zyapkov@gmail.com>

buildarch=2

pkgname=uboot-olinuxino
pkgdesc="U-Boot for i.MX233-OLinuXino"
pkgver=2014.04
pkgrel=1
arch=('arm')
license=('GPL')
#makedepends=('elftosb')
install=$pkgname.install
options=('!strip')
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
        "https://raw.github.com/eewiki/u-boot-patches/master/v${pkgver}/0001-mx23_olinuxino-uEnv.txt-bootz-n-fixes.patch"
        'alarm.patch'
        'uEnv.txt')
md5sums=('6d2116d1385a66e9a59742caa9d62a54'
         '71b2ee3b801b0265e5a316b9fc8d5f45'
         '6d183eca7b72b51c207e7fad23459659'
         '239b6223bbfe21e0cad629723a3abc2d')

prepare() {
  cd "$srcdir/u-boot-${pkgver}"
  
  patch -Np1 -i ..//0001-mx23_olinuxino-uEnv.txt-bootz-n-fixes.patch
  patch -Np1 -i ../alarm.patch
}

build() {
  cd "$srcdir/u-boot-${pkgver}"

  unset LDFLAGS CFLAGS CXXFLAGS

  make distclean
  make mx23_olinuxino_config
  make u-boot.sb
}

package() {
  mkdir -p "${pkgdir}/boot"
  cp "${srcdir}/u-boot-${pkgver}"/u-boot.sb "${srcdir}"/uEnv.txt "${pkgdir}/boot"
}
