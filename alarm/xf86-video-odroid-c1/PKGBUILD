# ODROID-C1 Mali Xorg Driver
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgname=xf86-video-odroid-c1
pkgver=0.5
pkgrel=1
pkgdesc="X.org Mali video driver for ODROID-C1"
arch=(armv7h)
license=('custom')
url="http://www.hardkernel.com/"
depends=('odroid-c1-libgl')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=18')
conflicts=('xorg-server<1.16' 'X-ABI-VIDEODRV_VERSION<18' 'X-ABI-VIDEODRV_VERSION>=19')
groups=('xorg-drivers' 'xorg')
source=("http://archlinuxarm.org/builder/src/maliddx-c1-${pkgver}.tar.gz")
sha256sums=('fc5db48a3cb83e52175e4f2f672af921a07148e6e41f52eef0a63a4b742fef74')

build() {
  cd maliddx
  LDFLAGS+=" -L/usr/lib/mali-egl"
  ./configure --prefix=/usr
  make
}

package() {
  cd maliddx
  make DESTDIR="${pkgdir}" install
  install -dm755 "${pkgdir}/etc/X11/xorg.conf.d"
  cp xorg.conf "${pkgdir}/etc/X11/xorg.conf.d/99-mali-c1.conf"
}
