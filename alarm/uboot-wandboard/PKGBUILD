# U-Boot: Wandboard
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgbase=uboot-wandboard
pkgname=('uboot-wandboard-solo' 'uboot-wandboard-dual' 'uboot-wandboard-quad')
pkgver=2014.01
pkgrel=1
arch=('armv7h')
url="http://git.denx.de/u-boot.git/"
license=('GPL')
makedepends=('git')
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
        "https://raw.github.com/eewiki/u-boot-patches/master/v${pkgver}/0001-wandboard-uEnv.txt-bootz-n-fixes.patch"
        'alarm.patch')
md5sums=('e531307578f6d32a7ccb1d04f1e08cbc'
         '07b1aba3db09dc86c2c3960baa4d77ad'
         '8f749cdb50b4deddbfa92655610ad5f4')

prepare() {
  cd u-boot-${pkgver}
  patch -p1 -i ../0001-wandboard-uEnv.txt-bootz-n-fixes.patch
  patch -p1 -i ../alarm.patch
}

build() {
  cd u-boot-${pkgver}
  
  unset CFLAGS CXXFLAGS LDFLAGS
  
  for i in solo dl quad; do
    make distclean
    make wandboard_${i}_config
    make
    mv u-boot.imx u-boot-${i}.imx
  done
}

package_uboot-wandboard-solo() {
  pkgdesc="U-Boot for Wandboard Solo"
  conflicts=('uboot-wandboard-dual' 'uboot-wandboard-quad')
  install=${pkgbase}.install

  mkdir -p "${pkgdir}"/boot
  cp u-boot-${pkgver}/u-boot-solo.imx "${pkgdir}"/boot/u-boot.imx
}

package_uboot-wandboard-dual() {
  pkgdesc="U-Boot for Wandboard Dual"
  conflicts=('uboot-wandboard-solo' 'uboot-wandboard-quad')
  install=${pkgbase}.install

  mkdir -p "${pkgdir}"/boot
  cp u-boot-${pkgver}/u-boot-dl.imx "${pkgdir}"/boot/u-boot.imx
}

package_uboot-wandboard-quad() {
  pkgdesc="U-Boot for Wandboard Quad"
  conflicts=('uboot-wandboard-solo' 'uboot-wandboard-dual')
  install=${pkgbase}.install

  mkdir -p "${pkgdir}"/boot
  cp u-boot-${pkgver}/u-boot-quad.imx "${pkgdir}"/boot/u-boot.imx
}
