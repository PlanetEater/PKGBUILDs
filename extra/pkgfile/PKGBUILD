# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - upstream patch, remove package when bumped

pkgname=pkgfile
pkgver=13
pkgrel=1.1
pkgdesc="a pacman .files metadata explorer"
arch=('i686' 'x86_64')
url="http://github.com/falconindy/pkgfile"
license=('MIT')
depends=('libarchive' 'curl' 'pcre' 'pacman')
source=("http://code.falconindy.com/archive/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig}
        'https://github.com/falconindy/pkgfile/commit/e66d942214143b2c105af3df7c7a7b961b80d832.patch')
install=pkgfile.install
md5sums=('7b4e20bf3ec6604cd439d5acd6ca871d'
         'SKIP'
         '7cfd0ab718d6cdaa674f57c89ca102d0')

prepare() {
  cd "$pkgname-$pkgver"
  patch -p1 -i ../e66d942214143b2c105af3df7c7a7b961b80d832.patch
}

build() {
  cd "$pkgname-$pkgver"

  ./configure
  make

  # generate a license file
  sed '/\*\//q' src/pkgfile.c >LICENSE
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ft=sh syn=sh
