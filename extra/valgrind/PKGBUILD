# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>
# Maintainer: Allan McRae <allan@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix building on armv5 and armv6
#  - drop openmpi support

noautobuild=1

pkgname=valgrind
pkgver=3.9.0
pkgrel=4
pkgdesc='A tool to help find memory-management problems in programs'
arch=(i686 x86_64)
license=(GPL)
url='http://valgrind.org/'
depends=('glibc>=2.19' 'glibc<2.20' 'perl')
makedepends=('gdb')
options=('staticlibs' '!emptydirs')
source=(http://valgrind.org/downloads/$pkgname-$pkgver.tar.bz2
        alarm.patch
        valgrind-3.9.0-glibc-2.19.patch)
sha1sums=('9415e28933de9d6687f993c4bb797e6bd49583f1'
         '65adc9cf0035ff8598f9997754527d64'
         '21020338ad5a93e6f9694946183e8835')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i "$srcdir"/valgrind-3.9.0-glibc-2.19.patch
  [ "${CARCH}" = "arm" -o "${CARCH}" = "armv6h" ] && patch -Np1 -i ${srcdir}/alarm.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
