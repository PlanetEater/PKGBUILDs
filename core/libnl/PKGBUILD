# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix assigning addresses on older kernels
#  - should be included in next release

pkgname=libnl
pkgver=3.2.24
pkgrel=1.1
pkgdesc="Library for applications dealing with netlink sockets"
arch=(i686 x86_64)
url="http://www.infradead.org/~tgr/libnl/"
license=(GPL)
depends=(glibc)
backup=(etc/libnl/classid etc/libnl/pktloc)
source=("$url/files/$pkgname-$pkgver.tar.gz"
        'https://github.com/thom311/libnl/commit/5206c050504f8676a24854519b9c351470fb7cc6.patch')
sha256sums=('fb8d6e5dc8af5b85bc6d00a71582a68a01e6a3f7d1664d4a646e289a99dd6816'
            '3366b70c6617e8a10cd2163048e4d138f3cadfbc90f2f6def89fdf3df6872a5a')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../5206c050504f8676a24854519b9c351470fb7cc6.patch
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --disable-static
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
